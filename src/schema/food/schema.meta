MetaLanguage food name Food parent meta toplevel <day> #:
  The constructs in Meta(Food).
config:

  abstract
  Construct _foodroot_ #:
    All constructs in Meta(Food) inherit from this abstract construct,
    so the Attribute definitions here are available in subconstructs.
    However, note that the existence of an Attribute here does NOT mean it
    is automatically included in the subconstruct ... the subconstruct
    must specify the primary attribute (and any customization of other
    values).
  config:
    
    secondary
    Attribute comment: : simple = <empty> aliases <#:> #:
      An arbitrary multi-line comment used to describe the purpose of the
      construct.

    secondary
    Attribute config: : complex = <empty> children <Construct> #:
      The complex block within which Construct/Attribute instances can be
      modified.

    secondary
    Attribute scope: : complex = <empty> aliases <::> #:
      The collection of subconstructs.

  end Construct _foodroot_;

  Construct day < _foodroot_ #:
    Construct for grouping meals by day.
  config:

    primary
    Attribute day : word = <auto> #:
      The day, in format YYYY-mm-dd or YYYYmmdd

    secondary
    Attribute time : word = <empty> aliases <:> #:
      When the meal was consumed.

    secondary
    Attribute location : id = <empty> aliases <@> #:
      Where the meal was consumed.

    Attribute comment:;
    Attribute config:;
    Attribute scope: children <meal|exercise>;

  end Construct day;

  Construct meal < _foodroot_ #:
    The core grouping construct, describing a meal consumed.
  config:

    primary
    Attribute meal : id = <auto> #:
      Some identifier for the meal. Not strictly needed.

    secondary
    Attribute time : word = <empty> aliases <%> #:
      When the meal was consumed.

    secondary
    Attribute calories : word = <empty> #:
      A means of providing an estimate of calories when the details of the
      meal are not known.

    secondary
    Attribute location : word = <empty> aliases <@> #:
      Where the meal was consumed.

    Attribute comment:;
    Attribute config:;
    Attribute scope: children <food>;

  end Construct meal;

  Construct food < _foodroot_ #:
    The core food construct, describing a single food item consumed.
  config:

    feature
    Attribute certainty : <certain|maybe|wag> = certain #:
      How certain we are in the food amount
       - certain means confident in values
       - maybe means confidence is uncertain, but more than wag
       - wag is wild-ass guess that could be totally off.

    primary
    Attribute food : word = <auto> replacer is #:
      The name of the food consumed
      TODO(wmh): Set up implicit redirect to 'is'
      
    secondary
    Attribute is : word = <required> #:
      The name of the food.
      TODO(wmh): Set up implicit redirect from id to is.

    secondary
    Attribute amount : word = <required> aliases <=> #:
      The amount consumed.

    secondary
    Attribute calories : word = <empty> #:
      If provided, no search for data is performed ... the explicitly
      specified calories is accounted for, but no other nutritional info is
      recorded.

    Attribute comment:;
    Attribute config:;
    
  end Construct food;

  Construct exercise < _foodroot_ #:
    The core exercise construct, describing a single exercise item consumed.
  config:

    primary
    Attribute exercise : word = <auto> #:
      The name of the exercise performed.

    secondary
    Attribute type : word = <required> aliases <:> #:
      The kind of exercise.
      TODO(wmh): Should this be a feature attribute?

    secondary
    Attribute time : word = <empty> aliases <%> #:
      When the meal was consumed.

    Attribute comment:;
    Attribute config:;
    Attribute scope:;
    
  end Construct exercise;

end MetaLanguage food;
