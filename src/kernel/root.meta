namespace metax.root #:
  The top-level classes that Meta provides.
scope:

  meta  
  class ObjectMetaRoot #:
    The root of the metaclass hierarchy in Meta. 

    There are two ways we can handle the interaction between metaclasses
    in Meta and those in the baselang:
      1) The meta-level metaclass is-a baselang-provided metaclass
      2) The meta-level metaclass has-a baselang-provided metaclass
    See ../../README.md for details. Currently implementing variant #1.
  parent<py> ^type
  parent<js> ^Object
  parent<cc> void
  assocs<cc>:
    std assoc string;
    std assoc vector;
    std assoc map;
  scope:

    field metaname : @str #:
      The name of the class being represented by this metaclass.
      TODO(wmh): This field is not needed in python ... need a way to
      have 'specific' fields. But we do want to define metaname() to return
      the name as stored in the type metaclass.
      TODO(wmh): This should be a readonly field, so no setter or reffer.

    field metabases : @vec<class> #:
      The parent classes of the class.
      TODO(wmh): This field is not needed in python ... need a way to
      have 'specific' fields.  But we do want to define metabases() to return
      the bases as stored in the type metaclass.
      TODO(wmh): This should be a readonly field, so no setter or reffer.

    field metasymbols : @map #:
      The symbols available within the class.
      TODO(wmh): This field is not needed in python ... need a way to
      have 'specific' fields.  But we do want to define metasymbols() to return
      the symbols as stored in the type metaclass.
      TODO(wmh): This should be a readonly field, so no setter or reffer.

    lifecycle #:
      Every user-defined class has an auto-generated metaclass created for it,
      and that metaclass inherits (eventually) from this class.  The meta
      compiler implicitly inserts a params: block in meta class initializers
      (if users define a meta-level lifecycle construct, they should not
      specify params:, as that will be an error).

      This signature is currently motivated by the signature of metaclasses in
      Python. As additional baselangs are added to Meta, we may need to
      generalize this implicit signature. Note that Javascript and C++ do not
      have metaclasses, so we are not constrained by these baselangs). But when
      we add in support for Java, we will need to establish whether
      java.lang.Class can be subclassed (or whether metax.root.ObjectMeta will
      need to act as a wrapper around a java.lang.Class instance) and how that
      influences this signature.
    params:
      var name : &str #:
        The name of the class being created
      var bases : &#vec<class> #:
        The parent classes of the class (instances of metaclasses)
      var symbols : &#map #:
        The symbols available within the class.
    super<py> (name, bases, symbols)
    scope<py>:
    scope<js>:
      this.metanameIs(name);
      this.metabasesIs(bases);
      this.metasymbolsIs(symbols);
    scope<cc>:
      this->metanameIs(name);
      this->metabasesIs(bases);
      this->metasymbolsIs(symbols);
    end lifecycle;

  end class ObjectMetaRoot;

  native _ scope<py>:
    /# NaN: float
    /#   The not-a-number value.
    NaN = float('nan')
  end;

  class Object #:
    Except in special circumstances, every class defined within Meta inherits
    from this class. Every base language provides a specialized implementation
    that defines functionality useful in implementing Meta-level semantics
    within that base language. Some of the methods defined here are present in
    all languages, some are only present in a subset of languages or in just one
    language.

    This class does NOT introduce any instance-level state (or meta-level state
    either), but does define a significant amount of instance-level and
    meta-level functionality available to every instance and subclass.

    Note that the metaparent of this class is always ObjectMetaRoot. Any
    class that inherits from Object will have a metaclass that inherits
    from ObjectMetaRoot.  Any user-defined meta class by default inherits
    from ObjectMetaRoot (semantics implemented in
    metax.oopl.ClassConstruct.metaClassInfo().

    Meta:suppress: JSC_UNKNOWN_EXPR_TYPE
  parent<py> ^object
  parent<js> ^Object
  parent<cc> void
  metaparent metax.root.ObjectMetaRoot
  assocs<py>:
    std assoc logging;
  assocs<cc>:
    cls assoc metax.root.ObjectMetaRoot;
  scope:

    static
    field Config : map<@str,@str> #:
      Initialized when it is first invoked. Intentionally NOT a meta field of
      Object because that would produce one instance per subclass, when the
      object should be shared amongst all classes and objects.
    scope:
      accessor get
      lazy<py>:
        /# TODO(wmh): Get this working in bazel!  Need to add to assocs
        /# and define a BUILD target ... in oopl.tgz?
        import metastrap
        /# print 'NOTE: Initializing metax.root.Config!'
        result = metastrap.Config()
        logging.info('metax.root.Object.Config: initialized to %s' % str(result))
        -> result
      lazy<cc>:
        std::cerr << "metax.root.ObjectMeta.Config() not yet implemented" << std::endl;
    end field Config;

    static
    field CLI : any #:
      Stores an instance of metax.cli.Values, providing access to the set of
      flags and args dictated by the command line. Also provides access to
      the instantiated Command (via CLI()._command_()).
      - This is currently typed as 'any' because there will be a circularity
        induced if we type it as metax.cli.Values. Need to establish
        whether this will be implementable in C++.
      - Initialized via metax.root.MetaObject.Initialize(), which is
        invoked by 'meta2'. However, we need a mechanism for initializing
        it in situations where this object is desired 'in the wild'
        without the compiler as intermediary.
      - TODO(wmh): Find a way to initialize this properly.

    static
    field ResourceMap : @map<@str,@str> #:
      Maps fully qualified resource names to explicit paths. The
      Object.Resource() method looks in this map before performing its
      default logic. Initialized by Object.Initialize().

    static
    method Init #:
      A means of initializing information that should be accessible from
      every meta-generated class.
    params:
      var cli : any = null #:
        A metax.cli.Values instance obtained from instantiating a command line.
        IMPORTANT: We are not specifying the type (metax.cli.Values) here because
        it would introduce a circularity to depend on metax.cli.Values.
      var resources : map<@str,@str> = null #:
        An explicit mapping for resources.
    scope<py>:
      if cli is not None:
        Object.CLIIs(cli)
        logging.info('metax.root.Object.Init: CLI initialized.')
      if resources is not None:
        Object.ResourceMap().update(resources)
        logging.info('metax.root.Object.Init: ResourceMap initialized.')
    scope<cc>:
      if (cli != nullptr) {
        Object::CLIIs(cli);
      }
      if (resources != nullptr) {
        Object::ResourceMapIs(resources);
      }
    end method Init;

    static
    method RegisterResource #:
      Add a resource to the resource map.
    params:
      var name : str #:
        Name of the resource
      var path : str #:
        The value associated with the resource.
    scope:
      Object.ResourceMap()[name] = path
    end;

    meta
    method Resource : str #:
      This method provides an interface by which a user an obtain a
      resource that was defined via the 'resource' construct within
      the 'assocs' attribute of a class.  Having this be a meta method allows
      us to:
       - store the links in class-specific directories without worrying that
         invocation from a subclass will break the naming.
       - allows resources from one class to be accessed from another class
       - will work with non-meta classes (assuming the non-meta BUILD
         files are properly defined).                                          
    params:
      var resource_id : str #:
        The id of the resource (that is, the value of the primary attribute
        of the 'resource' construct that defines the resource. This may be
        a id or an xid.
      var fqn : str = null #:
        The fully qualified name of the class for which resources are
        desired.  If null, and resource_id is not an fqn, uses the receiver
        cls to determine fqn.
      var test : bool = false #:
        If true, the resource is for a test class.
    scope<py>:
      /# TODO(wmh): Each metaclass should maintain the list of legal
      /# resources associated with class instances, so that this method
      /# is just a lookup in a map.  There are, however, some complexities
      /# involved in auto-generating this map that need to be worked out
      /# (code for auto-generation of field constructs needs to be written,
      /# similar to the code for auto-generating methods).  We also need
      /# to decide whether resources get inherited from parent classes, etc.

      /# One problem with this method is that it should really be defined
      /# in metax.c.Compiler, which has all the data and methods to make
      /# a simple determination of the namespace directory. But we do not
      /# want metax.root.Object depending on metax.c.Compiler.
      /#
      /# TODO(wmh): Do we want to provide access to ~/.config/metameta
      /# via a meta or static var on Object? That would give us access to
      /# repository_path without having to rely on METAREP

      /# TODO(wmh): In metax.c.*, there is a moratorium on the use of
      /# baselang I/O methods (everything must go thru Compiler.fs()). But
      /# if we don't apply the same rules here, we will fail to be able to
      /# provide a memory-only filesystem. Figure out how to handle this.
      import os
      if '.' in resource_id:
        if fqn:
          raise Error(
            'Do not specify both fqn (%s) and fully qualified resource_id (%s)'
            % (fqn, resource_id))
        fqn, resource_id = resource_id.rsplit('.', 1)
      elif not fqn:
        fqn = cls.__module__ + '.' + cls.__name__
      namespace_fqn, clsname = fqn.rsplit('.', 1)
      subparts = namespace_fqn.split('.')
      fqrn = fqn + '.' + resource_id

      result = None
      resmap = cls.ResourceMap()
      if resmap:
        result = resmap.get(fqrn, None)
      if result:
        /# We've found an explicit match for the named resource in the
        /# resources mapping, so we use it.
        /# logging.info('Found explicit "%s" for %s', result, fqrn)
        pass
      else:
        /# TODO(wmh): Get this working in bazel!  Need to add to assocs
        /# and define a BUILD target ... in oopl.tgz?
        import metastrap
        version = metastrap.Version
        if version is None:
          logging.warning('Implicitly invoking metastrap.Setup()')
          metastrap.Setup()
          version = metastrap.Version
      
        /# Convert the fqn to a relative path.
        if os.getenv('TEST_SRCDIR', None) or os.getenv('IN_UNITTEST'):
          /# We are in bazel running a test.  All such tests are invoked from
          /# the directory containing WORKSPACE.
          parts = [os.getcwd()] + subparts
        else:
          config = cls.Config()
          if version == 'beta':
            if config:
              metarep = config['repository_path']
            else:
              metarep = os.getenv('METAREP')
              if metarep is None:
                metarep = '/Users/wmh/src/wmh/lib/meta2'
                print 'WARNING: Temporarily using hardcoded repopath %s' % metarep
              else:
                metarep += '2'  # NEWSUFFIX
            parts = [metarep, 'oopl', 'python']
          else:
            parts = [config['src_root'], 'lib', 'versions', version, 'lib']
          parts.extend(subparts)
        /# CODETANGLE(resource_dir): In metax.c (parser.meta).
        parts.append('.__resources')
        /# CODETANGLE(resource_name): In metax.c (parser.meta).
        parts.append('%s_%s' % (clsname, resource_id))
        path = '/'.join(parts)
        /# NOTE: Although I used to return the non-realpath version, allowing
        /# the caller to decide how to proceed, there was never a time where I
        /# wanted the non-realpath version, so I've decided to always convert.
        /# This does introduce an inconsistency though, because paths that do not
        /# exist will stay in non-realpath format. 
        /# TODO(wmh): How to avoid using baselang I/O commands here??
        result = os.path.realpath(path)
      return result
    scope<cc>:
      return nullptr;
    scope<js>:
      return null;
    end method Resource;

    lifecycle #:
      Do NOT introduce any instance-level state here.
      DO NOT introduce any meta-level state either.
    scope:
    end lifecycle;

    remark hidden #:
      I am currently disabling the test class for Object because it adds to
      the complexity of bootstrapping javascript. Consider reinstating these.

      raw test field obj : metax.root.Object;

      test 
      lifecycle #:
        A test lifecycle that creates an Object instance.
      setup<py>:
        test.obj = metax.root.Object()
      setup<js>:
        test.obj = new metax.root.Object();
      end;

      test method test_generic
      scope<py>:
        print 'here with %s' % test.obj
      scope<js>:
        console.log('here with ' + test.obj);
      end;
    end remark hidden;

  end class Object;

  nometa
  class Error #:
    The exception hierarchy is documented in the following places:
      python: https://docs.python.org/2/library/exceptions.html
      javascript: https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Global_Objects/Error
  parent<py> ^Exception
  parent<js> ^Error
  parent<cc> ^std.runtime_error
  assocs<cc>:
    cls assoc ObjectMetaRoot;
    std assoc exception;
  scope:

    lifecycle #:
      Pass the message.
    params<cc>:
      var msg : str = null;
    super<cc> (msg)
    scope<cc>:
    end;

  end class Error;

  nometa
  class InternalError < Error;


end namespace metax.root;
